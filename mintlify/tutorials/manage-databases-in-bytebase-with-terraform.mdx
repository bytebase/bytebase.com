---
title: Manage Databases with Terraform
author: Adela
updated_at: 2025/07/10 21:15
tags: Tutorial
integrations: Terraform
category: 'Integration'
featured: true
level: Intermediate
estimated_time: '30 mins'
---

import TerminalDockerRunVolume from '/snippets/install/terminal-docker-run-volume.mdx';
import VCSWithNgrok from '/snippets/install/start-with-ngrok.mdx';

This tutorial is part of the **Manage Bytebase with Terraform** series:

- Part 1: Manage Databases with Terraform (This one) - Set up instances and environments
- Part 2: [Manage Projects with Terraform](/tutorials/manage-projects-in-bytebase-with-terraform) - Organize databases into projects
- Part 3: [Manage Bytebase Settings with Terraform](/tutorials/manage-bytebase-settings-with-terraform) - Configure workspace settings, environment policies, approval flows, and risk management
- Part 4: [Manage SQL Review Rules with Terraform](/tutorials/manage-sql-review-rules-with-terraform) - Set up SQL review policies
- Part 5: [Manage Database Access Control with Terraform](/tutorials/manage-database-access-control-with-terraform) - Set up access controls and permissions
- Part 6: [Manage Data Masking with Terraform](/tutorials/manage-data-masking-with-terraform) - Configure data masking policies

ðŸ“š [Complete tutorial terraform files on GitHub](https://github.com/bytebase/terraform-provider-bytebase/tree/main/tutorials)

**File Structure**: This tutorial series uses separate Terraform files for better organization. Files are numbered by tutorial part (e.g., `1-instances.tf` for Part 1, `2-projects.tf` for Part 2, etc.). Terraform automatically handles dependencies between files.

Learn how to automate database infrastructure management by combining [Bytebase](https://www.bytebase.com) with [Terraform](https://www.hashicorp.com/products/terraform) using the [Terraform Bytebase Provider](https://registry.terraform.io/providers/bytebase/bytebase/latest/docs).

<Note>

Bytebase Terraform Provider handles control plane configuration such as settings, policies, access controls. It does not
handle data plane operations such as database creation, schema migrations, DML execution, query.

</Note>

## What You'll Learn

- **Set up** Bytebase with Terraform for automated database management
- **Configure** environments using settings or individual resources
- **Create** database instances across test and production environments

## Why Use Terraform with Bytebase?

Terraform enables you to manage hundreds of database instances as code, eliminating manual configuration errors and ensuring consistency across all environments.

## Prerequisites

Before starting this tutorial, ensure you have:

- **Docker**: [Install Docker](https://www.docker.com/) to run Bytebase
- **Ngrok**: [Sign up for ngrok](https://ngrok.com/) to expose your local Bytebase instance to the internet
- **Terraform**: [Install Terraform](https://www.terraform.io/downloads) for infrastructure as code
- **Bytebase Enterprise Plan** (optional) â€“ Some features may require the Enterprise Plan. You can still follow this tutorial with the Community Plan by making a few adjustments.

## Setup

### Install Terraform

**For macOS (using Homebrew):**

```bash
# Install HashiCorp tap
brew tap hashicorp/tap

# Install Terraform
brew install hashicorp/tap/terraform

# Verify installation
terraform -help
```

**For other platforms:** Follow the [official Terraform installation guide](https://developer.hashicorp.com/terraform/tutorials/aws-get-started/install-cli).

### Start Bytebase with Ngrok

<VCSWithNgrok />

### Prepare some data

1. Visit Bytebase and register an admin account with `Workspace Admin` role.

   ![register-admin](/content/docs/tutorials/manage-databases-in-bytebase-with-terraform/bb-register-admin.webp)

1. Follow the guide to configure, you'll need the **built-in sample** data for this tutorial.

   ![built-in-instance](/content/docs/tutorials/manage-databases-in-bytebase-with-terraform/bb-use-built-in-sample.webp)

1. (Optional) After you log into Bytebase, click **Settings > Subscription** on the left sidebar, paste your Enterprise Plan licence and click **Upload License**. _Some features may require the Enterprise Plan. You can still follow this tutorial with the Community Plan by making a few adjustments._

   ![subscription](/content/docs/tutorials/manage-databases-in-bytebase-with-terraform/bb-subscription.webp)

### Check Bytebase Setup (GUI)

1. Click **Instances** on the left sidebar, you will see there are two PostgreSQL instances: `Prod Sample Instance` and `Test Sample Instance`. You may click **+ Add Instance** to add new instances.

   ![add-instance](/content/docs/tutorials/manage-databases-in-bytebase-with-terraform/bb-add-instance.webp)

1. Click **Projects** on the left sidebar, you will see there is a `Sample Project`, click it or go from the `Select Project` dropdown on the top bar.

1. Within the project, click **Database > Databases** on the left sidebar, you will see there are two databases: `hr_prod`(on `Prod Sample Instance`) and `test_prod`(on `Prod Sample Instance`). You may click **+ New DB** to create new databases.

   ![add-db](/content/docs/tutorials/manage-databases-in-bytebase-with-terraform/bb-add-db.webp)

Manual processes works fine for a few instances/databases, but imagine doing it for dozens or hundreds of instances/databases across multiple environments/projects!

## Configure Instances via Terraform

In this section, youâ€™ll witness the process simplification Terraform brings.

### Step 1 - Create Terraform Files

1. Create a new folder `learn-terraform-bytebase` and create the following files in it.

1. Create `0-provider.tf` for the provider configuration:

   ```hcl
   terraform {
     required_providers {
       bytebase = {
         source  = "registry.terraform.io/bytebase/bytebase"
         version = "3.8.0"  # Use the latest stable version
       }
     }
   }
   ```

1. Copy the following provider part and paste it in `0-provider.tf`.

   ```hcl
   provider "bytebase" {
     service_account = "tf@service.bytebase.com"
      service_key Â  Â  = "<Your Bytebase service account key>"
      url Â  Â  Â  Â  Â  Â  = "<Your Bytebase external URL>"
   }
   ```

### Step 2 - Add a Terraform Service Account

1. Click **IAM & Admin > Users & Groups** on the left sidebar, click **+ Add User**.

1. Choose `Service Account` as the **Type**, fill in the **Email** with `tf@service.bytebase.com`, choose `Workspace Admin` as **Roles**, and click **Confirm**.

1. Copy the **Service Key** for later use.

### Step 3 - Query to list all resources

1. Paste the **Service Key**, **Service Account Email**, and **URL** into `0-provider.tf`.

1. Create `1-0-list-env-instance.tf` to list all existing environments and instances:

   ```hcl
   # List all environments using settings
   data "bytebase_setting" "environments" {
      name = "settings/ENVIRONMENT"
   }
   output "all_environments" {
      value = data.bytebase_setting.environments
   }

   # List all instances
   data "bytebase_instance_list" "all" {}
   output "all_instances" {
      value = data.bytebase_instance_list.all
   }
   ```

1. Run `terraform init`, `terraform plan` and `terraform apply` one by one in the terminal. Youâ€™ll see:

   - two environments: `test` and `prod`.
   - two instances: `prod-sample-instance` with `hr_prod` database and `test-sample-instance` with `hr_test` database.

### Step 4 - Configure Environments

Before configuring instances, let's properly configure the environments. There are two approaches to achieve this - you typically only need one of them. Firstly, within Bytebase, click **Environments** on the left sidebar. You will see there are two default envrionments.

    ![env-default](/content/docs/tutorials/manage-databases-in-bytebase-with-terraform/bb-env-default.webp)

#### Step 4a - Configure Environment Settings (Recommended)

This approach uses environment settings and is usually sufficient for most use cases.

Create `1-1-env.tf` with the following environment configuration:

    ```hcl
    # Environment Settings
    resource "bytebase_setting" "environments" {
      name = "settings/ENVIRONMENT"

      environment_setting {
        environment {
          id        = "test"
          title     = "Test"
          protected = false
        }
        environment {
          id        = "prod"
          title     = "Prod"
          protected = true
        }
      }
    }
    ```

#### Step 4b - Create Environments via Individual Resources (Alternative)

Alternatively, you can create environments using individual `bytebase_environment` resources. This approach provides more granular control but requires careful dependency management.

**Important**: When using multiple `bytebase_environment` resources, you must use `depends_on` between environments. This ensures Terraform updates them in the correct order, as the Bytebase API only supports updating one environment list at a time.

Create `1-1-env-alt.tf` with the following environment resources (alternative approach):

    ```hcl
    # Create Test environment
    resource "bytebase_environment" "test" {
      resource_id             = "test"
      title                   = "Test"
      order                   = 0
      protected               = false
    }

    # Create Production environment
    # depends_on ensures environments are created in sequence
    # This prevents API conflicts when updating the environment list
    resource "bytebase_environment" "prod" {
      depends_on              = [bytebase_environment.test]
      resource_id             = "prod"
      title                   = "Prod"
      order                   = 1
      protected               = true
    }
    ```

### Step 5 - Add Database Instances

Finally, let's add the database instances that will be associated with our environments. The configuration depends on which approach you chose in Step 4:

#### If you used Step 4a (Environment Settings)

Create `1-2-instances.tf` with the following configuration:

```hcl
# Built-in Test Instance
resource "bytebase_instance" "test" {
  depends_on  = [bytebase_setting.environments]
  resource_id = "test-sample-instance"
  environment = "environments/test"
  title       = "Test Sample Instance"
  engine      = "POSTGRES"
  activation  = true

  data_sources {
    id       = "admin data source test-sample-instance"
    type     = "ADMIN"
    host     = "/tmp"
    port     = "8083"
    username = "bbsample"
    password = ""
  }
}

# Built-in Prod Instance
resource "bytebase_instance" "prod" {
  depends_on  = [bytebase_setting.environments]
  resource_id = "prod-sample-instance"
  environment = "environments/prod"
  title       = "Prod Sample Instance"
  engine      = "POSTGRES"
  activation  = true

  data_sources {
    id       = "admin data source prod-sample-instance"
    type     = "ADMIN"
    host     = "/tmp"
    port     = "8084"
    username = "bbsample"
    password = ""
  }
}
```

#### If you used Step 4b (Individual Environment Resources)

Create `1-2-instances-alt.tf` with the following configuration (alternative approach):

```hcl
  # Built-in Test Instance
  resource "bytebase_instance" "test" {
    depends_on  = [bytebase_environment.test]
    resource_id = "test-sample-instance"
    environment = bytebase_environment.test.name
    ...

    data_sources {
      ...
    }
  }

  # Built-in Prod Instance
  resource "bytebase_instance" "prod" {
    depends_on  = [bytebase_environment.prod]
    resource_id = "prod-sample-instance"
    environment = bytebase_environment.prod.name
    ...

    data_sources {
      ...
    }
  }
```

**Key Points**:

- **Step 4a approach**: Instances depend on `bytebase_setting.environments` and use environment references like `"test"`
- **Step 4b approach**: Instances depend on individual environment resources and use direct references like `bytebase_environment.test.name`
- Both approaches create the same end result - database instances properly linked to their environments

1. Run `terraform plan` and `terraform apply` one by one in the terminal.

1. Go back to Bytebase, and click **Environments**. The `Prod` will be set as protected with the shield icon.

1. Click **Instances** on the left sidebar, and you will see the two instances now activated with `Y` under License. You may change the instance name and then run terraform again.

## Next Steps

Continue the series [Manage Projects with Terraform](/tutorials/manage-projects-in-bytebase-with-terraform) - organize your databases into projects.

**Resources:**

- [Tutorial files](https://github.com/bytebase/terraform-provider-bytebase/tree/main/tutorials)
- [Advanced examples](https://github.com/bytebase/terraform-provider-bytebase/tree/main/examples)
- [Discord community](https://discord.gg/huyw7gRsyA)
