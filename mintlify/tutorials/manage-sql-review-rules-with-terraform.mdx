---
title: Manage SQL Review Rules with Terraform
author: Adela
updated_at: 2025/07/10 21:15
tags: Tutorial
integrations: Terraform
category: 'Integration'
featured: true
level: Intermediate
estimated_time: '20 mins'
---

This tutorial is part of the **Bytebase Terraform Provider** series:

- Part 1: [Manage Environments with Terraform](/tutorials/manage-environments-in-bytebase-with-terraform) - Set up environments with policies
- Part 2: [Manage Databases with Terraform](/tutorials/manage-databases-in-bytebase-with-terraform) - Register database instances
- Part 3: [Manage Projects with Terraform](/tutorials/manage-projects-in-bytebase-with-terraform) - Organize databases into projects
- Part 4: [Manage Bytebase Settings with Terraform](/tutorials/manage-bytebase-settings-with-terraform) - Configure workspace profile and approval policies
- üëâ Part 5: Manage SQL Review Rules with Terraform - Define SQL review policies
- Part 6: [Manage Users and Groups with Terraform](/tutorials/manage-users-and-groups-with-terraform) - Set up dummy users and groups
- Part 7: [Manage Database Access Control with Terraform](/tutorials/manage-database-access-control-with-terraform) - Define permissions
- Part 8: [Manage Data Masking with Terraform](/tutorials/manage-data-masking-with-terraform) - Protect sensitive data

<Card icon="code-xml" cta="View sample code" href="https://github.com/bytebase/terraform-provider-bytebase/tree/main/tutorials">

This tutorial series uses separate Terraform files for better organization. Files are numbered by tutorial part and sub-step (e.g., `1-1-env-setting.tf`, `1-2-env-policy-rollout.tf` for Part 1, `2-instances.tf` for Part 2, etc.). Terraform automatically handles dependencies between files.

</Card>

## What You'll Learn

- **Configure** SQL review rules for schema standards
- **Enforce** naming conventions and structural requirements
- **Apply** different severity levels (ERROR, WARNING)
- **Target** specific environments with review policies

## Prerequisites

Before starting this tutorial, ensure you have:

- Completed [Part 3: Manage Bytebase Settings with Terraform](/tutorials/manage-bytebase-settings-with-terraform)
- Bytebase running with service account configured
- Your Terraform files from the previous tutorials

## Setup

From the previous tutorials, you should have:

- Bytebase instances and projects configured
- Environments (test and prod) set up
- Workspace settings and approval flows configured

## Configure SQL Review Rules

### Step 1 - Create Review Configuration

Create `4-sql-review.tf` with the SQL review configuration:

```hcl
resource "bytebase_review_config" "sample" {
  depends_on = [
    bytebase_setting.environments
  ]

  resource_id = "review-config-sample"
  title       = "Sample SQL Review Config"
  enabled     = true
  resources = toset([
    bytebase_setting.environments.environment_setting[0].environment[1].name
  ])

  # Rule 1: Warn about nullable columns
  rules {
    type   = "column.no-null"
    engine = "POSTGRES"
    level  = "WARNING"
  }

  # Rule 2: Require specific columns
  rules {
    type    = "column.required"
    engine  = "POSTGRES"
    level   = "ERROR"
    payload = "{\"list\":[\"id\",\"created_ts\",\"updated_ts\",\"creator_id\",\"updater_id\"]}"
  }

  # Rule 3: Tables must have primary key
  rules {
    type   = "table.require-pk"
    engine = "POSTGRES"
    level  = "ERROR"
  }

  # Rule 4: Column naming convention
  rules {
    type    = "naming.column"
    engine  = "POSTGRES"
    level   = "ERROR"
    payload = "{\"format\":\"^[a-z]+(_[a-z]+)*$\",\"maxLength\":64}"
  }

  # Rule 5: Limit maximum rows in SELECT
  rules {
    type    = "statement.maximum-limit-value"
    engine  = "POSTGRES"
    level   = "ERROR"
    payload = "{\"number\":1000}"
  }
}
```

### Step 2 - Apply Configuration

```bash
terraform plan
terraform apply
```

### Step 3 - Verify in Bytebase

1. Go to **CI/CD > SQL Review** in the left sidebar, you should see **Sample SQL Review Config** listed.

   ![sql-review-list](/content/docs/tutorials/manage-sql-review-rules-with-terraform/bb-sql-review-list.webp)

1. Click on it to view the configured rules. Note that it's applied to the production environment.

   ![sql-review-pg](/content/docs/tutorials/manage-sql-review-rules-with-terraform/bb-sql-review-pg.webp)

### Rule Types Explained

The example demonstrates key SQL review rule categories:

#### 1. Column Rules

- **column.no-null**: Warns about nullable columns
- **column.required**: Enforces required columns (id, timestamps, audit fields)

#### 2. Table Rules

- **table.require-pk**: Every table must have a primary key

#### 3. Naming Rules

- **naming.column**: Enforces lowercase snake_case column names

#### 4. Statement Rules

- **statement.maximum-limit-value**: Prevents SELECT queries without proper limits

### Step 4 - Test SQL Review

Create a test SQL statement to see the review in action:

1.  Go to **Project Two**, and click **Database > Databases** on the left sidebar.
1.  Check `hr_prod` and click **Edit Schema**.
1.  Try creating a table that violates the rules:

    ```sql
    -- This will trigger multiple violations
    CREATE TABLE BadExample (
        FirstName VARCHAR(50),  -- Violates naming convention
        LastName VARCHAR(50)    -- Missing required columns, no primary key
    );
    ```

    Expected violations:

         - ‚ùå Column naming convention (should be first_name)
         - ‚ùå Missing required columns (id, created_ts, etc.)
         - ‚ùå No primary key defined

    ![sql-review-violations](/content/docs/tutorials/manage-sql-review-rules-with-terraform/bb-sql-review-violations.webp)

1.  Even if you continue to create an issue, it will still show red SQL Review.

    ![issue-sql-review-errors](/content/docs/tutorials/manage-sql-review-rules-with-terraform/bb-issue-sql-review-errors.webp)

1.  Try a compliant table:

    ```sql
    -- This follows all rules
    CREATE TABLE good_example (
        id SERIAL PRIMARY KEY,
        first_name VARCHAR(50) NOT NULL,
        last_name VARCHAR(50) NOT NULL,
        created_ts TIMESTAMP NOT NULL DEFAULT NOW(),
        updated_ts TIMESTAMP NOT NULL DEFAULT NOW(),
        creator_id INTEGER NOT NULL,
        updater_id INTEGER NOT NULL
    );
    ```

    Expected result:

    - ‚úÖ All required rules pass

    ![issue-sql-review-pass](/content/docs/tutorials/manage-sql-review-rules-with-terraform/bb-issue-sql-review-pass.webp)

## Advanced Configuration

### Targeting Multiple Environments

To apply rules to multiple environments:

    ```hcl
    resources = toset([
      "environments/test",
      "environments/prod"
    ])
    ```

### Engine-Specific Rules

Different rules for different database engines:

    ```hcl
    # MySQL-specific rule
    rules {
      type   = "table.require-pk"
      engine = "MYSQL"
      level  = "ERROR"
    }

    # PostgreSQL-specific rule

    rules {
    type = "table.require-pk"
    engine = "POSTGRES"
    level = "ERROR"
    }

    ```

## Key Points

- **Rule Levels**: ERROR blocks changes, WARNING allows but notifies
- **Engine Specific**: Rules can target specific database engines
- **Environment Scoped**: Apply different rules to different environments
- **Payload Format**: Complex rules use JSON payloads for configuration

## Next Steps

**Continue the series:** [Manage Database Access Control with Terraform](/tutorials/manage-database-access-control-with-terraform) - set up access controls and permissions.

**Resources:**

- [SQL Review Rules Documentation](https://www.bytebase.com/docs/sql-review/review-rules)
- [Tutorial files](https://github.com/bytebase/terraform-provider-bytebase/tree/main/tutorials)
- [Advanced examples](https://github.com/bytebase/terraform-provider-bytebase/tree/main/examples)
- [Discord community](https://discord.gg/huyw7gRsyA)
