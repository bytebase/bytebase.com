---
title: Manage Bytebase Settings with Terraform
author: Adela
updated_at: 2025/07/10 21:15
tags: Tutorial
integrations: Terraform
category: 'Integration'
featured: true
level: Intermediate
estimated_time: '25 mins'
---

This tutorial is part of the **Manage Bytebase with Terraform** series:

- Part 1: [Manage Databases with Terraform](/tutorials/manage-databases-in-bytebase-with-terraform) - Set up instances and environments
- Part 2: [Manage Projects with Terraform](/tutorials/manage-projects-in-bytebase-with-terraform) - Organize databases into projects
- Part 3: Manage Bytebase Settings with Terraform (This one) - Configure workspace settings, approval flows, and risk management
- Part 4: [Manage Database Access Control with Terraform](/tutorials/manage-database-access-control-with-terraform) - Set up access controls and permissions

ðŸ“š [Complete tutorial terraform files on GitHub](https://github.com/bytebase/terraform-provider-bytebase/tree/main/tutorials)

Learn how to configure workspace settings, approval flows, and risk policies using Terraform and the [Terraform Bytebase Provider](https://registry.terraform.io/providers/bytebase/bytebase/latest/docs).

<Note>

Bytebase Terraform Provider handles control plane configuration such as settings, policies, access controls. It does not
handle data plane operations such as database creation, schema migrations, DML execution, query.

</Note>

## What You'll Learn

- **Configure** workspace profile settings including signup controls and external URL
- **Set up** multi-step approval flows for database changes
- **Define** risk policies to trigger appropriate approval workflows

## Prerequisites

Before starting this tutorial, ensure you have:

- Completed [Part 2: Manage Projects with Terraform](/tutorials/manage-projects-in-bytebase-with-terraform)
- Bytebase running with ngrok and service account configured
- Your `main.tf` file from the previous tutorials

## Setup

From the previous tutorials, you should have:

- Bytebase instances and projects configured
- Service account with Workspace Admin role
- Your `main.tf` file ready for additional configurations

## Configure Bytebase Settings

### Step 1 - Workspace Profile Configuration

Add the workspace profile settings to your `main.tf`:

```hcl
# Step 1: Workspace profile configuration
resource "bytebase_setting" "workspace_profile" {
  name = "settings/WORKSPACE_PROFILE"

  workspace_profile {
    disallow_signup          = true
    domains                  = ["example.com"]
    enforce_identity_domain  = false
    external_url             = "https://xxx.ngrok-free.app"
  }
}
```

This configuration:

- Disables public signup for security
- Restricts users to specific email domains
- Sets your ngrok URL as the external URL

### Step 2 - Approval Flow Settings

Add approval flow configuration that requires multiple approvals for risky operations:

```hcl
# Step 2: Approval flow settings
resource "bytebase_setting" "approval_flow" {
  name = "settings/WORKSPACE_APPROVAL"

  approval_flow {
    rules {
      flow {
        title       = "Project Owner â†’ DBA â†’ Admin"
        description = "Need DBA and workspace admin approval"

        steps { role = "roles/projectOwner" }
        steps { role = "roles/workspaceDBA" }
        steps { role = "roles/workspaceAdmin" }
      }
      conditions {
        source = "DML"
        level  = "MODERATE"
      }
      conditions {
        source = "DDL"
        level  = "HIGH"
      }
    }
  }
}
```

This creates a three-step approval flow that triggers for:

- DML operations with MODERATE risk level
- DDL operations with HIGH risk level

### Step 3 - Risk Management Policies

Define risk policies that determine when approval flows are triggered:

```hcl
# Step 3: Risk management policies
resource "bytebase_risk" "dml_moderate" {
  title     = "DML Moderate Risk"
  source    = "DML"
  level     = 200
  active    = true
  condition = "environment_id == \"prod\" && affected_rows >= 100"
}

resource "bytebase_risk" "ddl_high" {
  title     = "DDL High Risk"
  source    = "DDL"
  level     = 300
  active    = true
  condition = "environment_id == \"prod\""
}
```

Risk levels:

- **200 (MODERATE)**: DML operations in production affecting 100+ rows
- **300 (HIGH)**: All DDL operations in production

### Step 4 - Apply Configuration

```bash
terraform plan
terraform apply
```

### Step 5 - Verify in Bytebase

1. Go to **Settings > General** to verify workspace profile settings.
1. Go to **CI/CD > Custom Approval** to see the approval flow.

   ![custom-approval](/content/docs/tutorials/manage-bytebase-settings-with-terraform/bb-custom-approval.png)

1. Go to **CI/CD > Risk Center** to view risk policies.

   ![risks](/content/docs/tutorials/manage-bytebase-settings-with-terraform/bb-risks.png)

1. Go into `Project Two`, click **Database > Databases** on the left sidebar. Check `hr_prod`, click **Edit Schema**, add a new table `t0`. After creating the issue, corresponding risk `DDL high` and approval flow `Project Owner â†’ DBA â†’ Admin` will be matched.

   ![issue-risk-high](/content/docs/tutorials/manage-bytebase-settings-with-terraform/bb-issue-risk-high.png)

## Key Points

- **Workspace Profile**: Controls signup, domain restrictions, and external URL
- **Approval Flows**: Define multi-step approval processes for risky operations
- **Risk Policies**: Automatically assess operations and trigger appropriate workflows
- You can configure additional [settings](https://registry.terraform.io/providers/bytebase/bytebase/latest/docs/resources/setting) such as classification and semantic_types. These will be covered in upcoming tutorials

## Next Steps

**Continue the series:** [Manage Database Access Control with Terraform](/tutorials/manage-database-access-control-with-terraform) - set up access controls and permissions.

**Resources:**

- [Tutorial files](https://github.com/bytebase/terraform-provider-bytebase/tree/main/tutorials)
- [Advanced examples](https://github.com/bytebase/terraform-provider-bytebase/tree/main/examples)
- [Discord community](https://discord.gg/huyw7gRsyA)
