---
title: Manage Bytebase Settings with Terraform
author: Adela
updated_at: 2025/07/10 21:15
tags: Tutorial
integrations: Terraform
category: 'Integration'
featured: true
level: Intermediate
estimated_time: '25 mins'
---

This tutorial is part of the **Bytebase Terraform Provider** series:

- Part 1: [Manage Databases with Terraform](/tutorials/manage-databases-in-bytebase-with-terraform) - Set up instances and environments
- Part 2: [Manage Projects with Terraform](/tutorials/manage-projects-in-bytebase-with-terraform) - Organize databases into projects
- ðŸ‘‰ Part 3: Manage Bytebase Settings with Terraform - Configure workspace settings, environment policies, approval flows, and risk management
- Part 4: [Manage SQL Review Rules with Terraform](/tutorials/manage-sql-review-rules-with-terraform) - Set up SQL review policies
- Part 5: [Manage Database Access Control with Terraform](/tutorials/manage-database-access-control-with-terraform) - Set up access controls and permissions
- Part 6: [Manage Data Masking with Terraform](/tutorials/manage-data-masking-with-terraform) - Configure data masking policies

ðŸ“š [Complete tutorial terraform files on GitHub](https://github.com/bytebase/terraform-provider-bytebase/tree/main/tutorials)

**File Structure**: This tutorial series uses separate Terraform files for better organization. Files are numbered by tutorial part and sub-step (e.g., `1-1-env.tf`, `1-2-instances.tf` for Part 1, `2-projects.tf` for Part 2, etc.). Terraform automatically handles dependencies between files.

## What You'll Learn

- **Configure** workspace profile settings including signup controls and external URL
- **Set up** environment-specific policies for test and production
- **Create** multi-step approval flows for database changes
- **Define** risk policies to trigger appropriate approval workflows

## Prerequisites

Before starting this tutorial, ensure you have:

- Completed [Part 2: Manage Projects with Terraform](/tutorials/manage-projects-in-bytebase-with-terraform)
- Bytebase running with ngrok and service account configured
- Your Terraform files from the previous tutorials

## Setup

From the previous tutorials, you should have:

- Bytebase instances and projects configured
- Service account with Workspace Admin role
- Your Terraform files ready for additional configurations

## Configure Bytebase Settings

### Step 1 - Workspace Profile Configuration

Create `3-1-workspace-profile.tf` with the workspace profile settings:

```hcl
# Step 1: Workspace profile configuration
resource "bytebase_setting" "workspace_profile" {
  name = "settings/WORKSPACE_PROFILE"

  workspace_profile {
    disallow_signup          = true
    domains                  = ["example.com"]
    enforce_identity_domain  = false
    external_url             = "https://xxx.ngrok-free.app"
  }
}
```

This configuration:

- Disables public signup for security
- Restricts users to specific email domains
- Sets your ngrok URL as the external URL

### Step 2 - Environment Policies

Create `3-2-env-setting.tf` to configure different policies for test and production environments:

```hcl
# Environment-specific policies
resource "bytebase_policy" "rollout_test" {
  depends_on = [
    bytebase_setting.environments
  ]

  parent              = "environments/test"
  type                = "ROLLOUT"
  enforce             = true
  inherit_from_parent = true

  rollout_policy {
    automatic = true
    workspace_roles = [
      "roles/workspaceAdmin",
      "roles/workspaceDBA",
      "roles/projectOwner",
      "roles/LAST_APPROVER",
      "roles/CREATOR"
    ]
    project_roles = [
      "roles/projectOwner",
      "roles/projectReleaser"
    ]
  }
}

resource "bytebase_policy" "rollout_prod" {
  depends_on = [
    bytebase_setting.environments
  ]

  parent              = "environments/prod"
  type                = "ROLLOUT"
  enforce             = true
  inherit_from_parent = true

  rollout_policy {
    automatic = false
    workspace_roles = [
      "roles/workspaceAdmin",
      "roles/workspaceDBA",
      "roles/projectOwner",
      "roles/LAST_APPROVER",
      "roles/CREATOR"
    ]
    project_roles = [
      "roles/projectOwner",
      "roles/projectReleaser"
    ]
  }
}

resource "bytebase_policy" "disable_copy_data_prod" {
  depends_on = [
    bytebase_setting.environments
  ]

  parent              = "environments/prod"
  type                = "DISABLE_COPY_DATA"
  enforce             = true
  inherit_from_parent = true

  disable_copy_data_policy {
    active = true
  }
}

resource "bytebase_policy" "data_source_query_prod" {
  depends_on = [
    bytebase_setting.environments
  ]

  parent              = "environments/prod"
  type                = "DATA_SOURCE_QUERY"
  enforce             = true
  inherit_from_parent = true

  data_source_query_policy {
    admin_data_source_restriction = "RESTRICTION_UNSPECIFIED"
  }
}
```

This configuration sets up:

- **Test Environment**: Automatic rollout enabled for faster development cycles
- **Production Environment**: Manual rollout for controlled releases
- **Copy Data Disabled**: Prevents copying production data for security
- **Data Source Query Restrictions**: Controls admin-level database queries

### Step 3 - Approval Flow Settings

Create `3-4-approval-flow.tf` with approval flow configuration that requires multiple approvals for risky operations:

```hcl
# Step 2: Approval flow settings
resource "bytebase_setting" "approval_flow" {
  name = "settings/WORKSPACE_APPROVAL"

  approval_flow {
    rules {
      flow {
        title       = "Project Owner â†’ DBA â†’ Admin"
        description = "Need DBA and workspace admin approval"

        steps { role = "roles/projectOwner" }
        steps { role = "roles/workspaceDBA" }
        steps { role = "roles/workspaceAdmin" }
      }
      conditions {
        source = "DML"
        level  = "MODERATE"
      }
      conditions {
        source = "DDL"
        level  = "HIGH"
      }
    }
  }
}
```

This creates a three-step approval flow that triggers for:

- DML operations with MODERATE risk level
- DDL operations with HIGH risk level

### Step 4 - Risk Management Policies

Create `3-3-risk.tf` with risk policies that determine when approval flows are triggered:

```hcl
# Step 3: Risk management policies
resource "bytebase_risk" "dml_moderate" {
  title     = "DML Moderate Risk"
  source    = "DML"
  level     = 200
  active    = true
  condition = "environment_id == \"prod\" && affected_rows >= 100"
}

resource "bytebase_risk" "ddl_high" {
  title     = "DDL High Risk"
  source    = "DDL"
  level     = 300
  active    = true
  condition = "environment_id == \"prod\""
}
```

Risk levels:

- **200 (MODERATE)**: DML operations in production affecting 100+ rows
- **300 (HIGH)**: All DDL operations in production

### Step 5 - Apply Configuration

```bash
terraform plan
terraform apply
```

### Step 6 - Verify in Bytebase

1. Go to **Settings > General** to verify workspace profile settings.
1. Go to **CI/CD > Custom Approval** to see the approval flow.

   ![custom-approval](/content/docs/tutorials/manage-bytebase-settings-with-terraform/bb-custom-approval.webp)

1. Go to **CI/CD > Risk Center** to view risk policies.

   ![risks](/content/docs/tutorials/manage-bytebase-settings-with-terraform/bb-risks.webp)

1. Go into `Project Two`, click **Database > Databases** on the left sidebar. Check `hr_prod`, click **Edit Schema**, add a new table `t0`. After creating the issue, corresponding risk `DDL high` and approval flow `Project Owner â†’ DBA â†’ Admin` will be matched.

   ![issue-risk-high](/content/docs/tutorials/manage-bytebase-settings-with-terraform/bb-issue-risk-high.webp)

## Key Points

- **Workspace Profile**: Controls signup, domain restrictions, and external URL
- **Environment Policies**: Configure different rollout, data copy, and query policies for test vs production
- **Approval Flows**: Define multi-step approval processes for risky operations
- **Risk Policies**: Automatically assess operations and trigger appropriate workflows
- You can configure additional [settings](https://registry.terraform.io/providers/bytebase/bytebase/latest/docs/resources/setting) such as classification and semantic_types. These will be covered in upcoming tutorials

## Next Steps

**Continue the series:** [Manage SQL Review Rules with Terraform](/tutorials/manage-sql-review-rules-with-terraform) - set up SQL review policies.

**Resources:**

- [Tutorial files](https://github.com/bytebase/terraform-provider-bytebase/tree/main/tutorials)
- [Advanced examples](https://github.com/bytebase/terraform-provider-bytebase/tree/main/examples)
- [Discord community](https://discord.gg/huyw7gRsyA)
