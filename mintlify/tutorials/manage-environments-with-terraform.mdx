---
title: Manage Environments with Terraform
author: Adela
updated_at: 2025/07/15 21:15
tags: Tutorial
integrations: Terraform
category: 'Integration'
featured: true
level: Intermediate
estimated_time: '30 mins'
---

import TerminalDockerRunVolume from '/snippets/install/terminal-docker-run-volume.mdx';
import TerraformGitHubSample from '/snippets/tutorials/terraform-github-sample.mdx';

This tutorial is part of the **Bytebase Terraform Provider** series:

- Part 1: Manage Environments with Terraform üëà
- Part 2: [Manage Databases with Terraform](/tutorials/manage-databases-with-terraform) - Register database instances
- Part 3: [Manage Projects with Terraform](/tutorials/manage-projects-with-terraform) - Organize databases into projects
- Part 4: [Manage Bytebase Settings with Terraform](/tutorials/manage-general-settings-with-terraform) - Configure workspace profile and approval policies
- Part 5: [Manage SQL Review Rules with Terraform](/tutorials/manage-sql-review-rules-with-terraform) - Define SQL review policies
- Part 6: [Manage Users and Groups with Terraform](/tutorials/manage-users-and-groups-with-terraform) - Configure users and groups
- Part 7: [Manage Database Access Control with Terraform](/tutorials/manage-database-access-control-with-terraform) - Grant database permissions
- Part 8: [Manage Data Masking with Terraform](/tutorials/manage-data-masking-with-terraform) - Protect sensitive data

<TerraformGitHubSample />

## What You'll Learn

In this tutorial, you'll use Terraform to manage Bytebase environments programmatically. You'll learn how to:

- Define environments (Test, Prod) with different protection levels
- Configure automatic vs manual deployment policies
- Set up data access restrictions for production
- Manage everything via Infrastructure as Code (IaC)

## Prerequisites

Before starting this tutorial, ensure you have:

- **Docker**: [Install Docker](https://www.docker.com/) to run Bytebase
- **Terraform**: [Install Terraform](https://www.terraform.io/downloads) (version 1.0+)
- **Bytebase Enterprise Plan** (optional): For advanced features

## Step 1 - Setup

### Install Terraform

Follow the [official Terraform installation guide](https://developer.hashicorp.com/terraform/tutorials/aws-get-started/install-cli).

### Start Bytebase

1. Run Bytebase in Docker:

   <TerminalDockerRunVolume />

1. Access Bytebase at `http://localhost:8080`.

1. Register an admin account with `Workspace Admin` role.

   ![register-admin](/content/docs/tutorials/manage-environments-with-terraform/bb-register-admin.webp)

1. Complete the setup to configure Bytebase, you'll need to select **use built-in sample** for this tutorial.

   ![use-built-in-sample](/content/docs/tutorials/manage-environments-with-terraform/bb-use-built-in-sample.webp)

1. (Optional) After logging into Bytebase, [activate official or trial license](/administration/license#configure-workspace-license). _Some features require the Enterprise Plan._

   ![subscription](/content/docs/tutorials/manage-environments-with-terraform/bb-subscription.webp)

### Explore Current Environments

Navigate to **Environments** in Bytebase. You'll see two default environments: `Test` and `Prod`. We‚Äôll manage these environments using Terraform next.

![env-default](/content/docs/tutorials/manage-environments-with-terraform/bb-env-default.webp)

## Step 2 - Configure Terraform Provider

### Set up the Provider

1. Create a new folder `learn-terraform-bytebase` and navigate to it.

1. Create `0-provider.tf`, visit [Terraform Bytebase Provider](https://registry.terraform.io/providers/bytebase/bytebase/latest/docs), click **USE PROVIDER** and copy the configuration.

   ![tf-use-provider](/content/docs/tutorials/manage-environments-with-terraform/tf-use-provider.webp)

   ```hcl 0-provider.tf
   terraform {
      required_providers {
         bytebase = {
            source  = "registry.terraform.io/bytebase/bytebase"
            version = "3.8.6"  # Check for latest version
         }
      }
   }

   provider "bytebase" {
      service_account = "tf@service.bytebase.com"
      service_key     = "<Your service key>"      # We'll get this next
      url             = "http://localhost:8080"    # Your Bytebase URL
   }
   ```

### Create a Service Account

1. In Bytebase, go to **IAM & Admin > Users & Groups**.
1. Click **+ Add User** and create a service account:
   - Type: `Service Account`
   - Email: `tf@service.bytebase.com`
   - Roles: `Workspace Admin`
1. Copy the generated **Service Key**.

### Initialize Terraform

1. Update `0-provider.tf` with your service account key.
2. Initialize Terraform:

```bash
terraform init
```

You should see: "Terraform has been successfully initialized!"

## Step 3 - Inspect Current Environments

Before making any changes, let's see what environments currently exist.

|                       |                                                                                                                        |
| --------------------- | ---------------------------------------------------------------------------------------------------------------------- |
| Terraform data source | [bytebase_environment](https://registry.terraform.io/providers/bytebase/bytebase/latest/docs/data-sources/environment) |
| Sample file           | [1-0-list-env.tf](https://github.com/bytebase/terraform-provider-bytebase/blob/main/tutorials/1-0-list-env.tf)         |

Create `1-0-list-env.tf`:

```hcl 1-0-list-env.tf
# Read current environment settings from Bytebase
data "bytebase_setting" "environments" {
   name = "settings/ENVIRONMENT"
}

# Display all environments
output "all_environments" {
   value = data.bytebase_setting.environments
}
```

Run these commands:

```bash
terraform plan
terraform apply
```

You'll see the existing `test` and `prod` environments.

## Step 4 - Define the Environment Configuration

|                    |                                                                                                                      |
| ------------------ | -------------------------------------------------------------------------------------------------------------------- |
| Terraform resource | [bytebase_setting](https://registry.terraform.io/providers/bytebase/bytebase/latest/docs/resources/setting)          |
| Sample file        | [1-1-env-setting.tf](https://github.com/bytebase/terraform-provider-bytebase/blob/main/tutorials/1-1-env-setting.tf) |

Create `1-1-env-setting.tf`:

```hcl 1-1-env-setting.tf
# Define environments via Infrastructure as Code
resource "bytebase_setting" "environments" {
   name = "settings/ENVIRONMENT"

   environment_setting {
      # Test environment - for development
      environment {
         id        = "test"
         title     = "Test"
         protected = false
      }

      # Production environment - needs protection
      environment {
         id        = "prod"
         title     = "Prod"
         ## Bytebase will attach a shield icon üõ°Ô∏è beside the environment name.
         protected = true
      }
   }
}
```

## Step 5 - Configure Environment Policies

Let's add rollout and data protection policies, for more details, see: [Environment Policy](/change-database/environment-policy/overview) and [Rollout Policy](/change-database/environment-policy/rollout-policy).

### Rollout Policy

|                    |                                                                                                                                    |
| ------------------ | ---------------------------------------------------------------------------------------------------------------------------------- |
| Terraform resource | [bytebase_policy](https://registry.terraform.io/providers/bytebase/bytebase/latest/docs/resources/policy)                          |
| Sample file        | [1-2-env-policy-rollout.tf](https://github.com/bytebase/terraform-provider-bytebase/blob/main/tutorials/1-2-env-policy-rollout.tf) |

When no rollout policy is found for an environment, Bytebase applies a default rollout policy with the following checkers:

- **Required Issue Approval**: Changes must be approved before deployment
- **Plan Check Enforcement**: SQL plan checks must pass (errors only)

You can explicitly configure these policies using Terraform. Create `1-2-env-policy-rollout.tf`:

```hcl 1-2-env-policy-rollout.tf
# Test environment - automatic deployment with default checkers
resource "bytebase_policy" "rollout_policy_test" {
   depends_on = [bytebase_setting.environments]
   parent     = bytebase_setting.environments.environment_setting[0].environment[0].name
   type       = "ROLLOUT_POLICY"

   rollout_policy {
      automatic = true  # Deploy changes automatically
      roles = [
         "roles/workspaceAdmin",
         "roles/projectOwner",
         "roles/LAST_APPROVER",
         "roles/CREATOR"
      ]

      # Default checkers (explicitly configured)
      checkers {
         required_issue_approval = true
         required_status_checks {
            plan_check_enforcement = "ERROR_ONLY"  # Block on errors only
         }
      }
   }
}

# Production - manual deployment with stricter checks
resource "bytebase_policy" "rollout_policy_prod" {
   depends_on = [bytebase_setting.environments]
   parent     = bytebase_setting.environments.environment_setting[0].environment[1].name
   type       = "ROLLOUT_POLICY"

   rollout_policy {
      automatic = false  # Require manual deployment
      roles = [
         "roles/workspaceAdmin",
         "roles/projectOwner",
         "roles/LAST_APPROVER",
         "roles/CREATOR"
      ]

      # Enforce all plan checks (errors and warnings)
      checkers {
         required_issue_approval = true
         required_status_checks {
            plan_check_enforcement = "STRICT"  # Block on both errors and warnings
         }
      }
   }
}
```

**Key Configuration Options:**

- `automatic`: When `true`, changes deploy automatically after approval. When `false`, requires manual click to deploy.
- `roles`: List of roles allowed to manually deploy changes. Required even with automatic rollout, as manual approval is needed when checks fail.
- `checkers.required_issue_approval`: When `true`, requires issue approval before rollout.
- `checkers.required_status_checks.plan_check_enforcement`: Controls SQL plan check enforcement:
  - `PLAN_CHECK_ENFORCEMENT_UNSPECIFIED`: Allow rollout regardless of plan check results (no enforcement)
  - `ERROR_ONLY`: Block rollout only when plan check finds errors (default)
  - `STRICT`: Block rollout when plan check finds errors or warnings (stricter for production)

### Data Protection Policy

|                    |                                                                                                                              |
| ------------------ | ---------------------------------------------------------------------------------------------------------------------------- |
| Terraform resource | [bytebase_policy](https://registry.terraform.io/providers/bytebase/bytebase/latest/docs/resources/policy)                    |
| Sample file        | [1-3-env-policy-data.tf](https://github.com/bytebase/terraform-provider-bytebase/blob/main/tutorials/1-3-env-policy-data.tf) |

Create `1-3-env-policy-data.tf`:

```hcl 1-3-env-policy-data.tf
# Prevent copying data in SQL Editor from production
resource "bytebase_policy" "disable_copy_data_policy_prod" {
   depends_on = [bytebase_setting.environments]
   parent     = bytebase_setting.environments.environment_setting[0].environment[1].name
   type       = "DISABLE_COPY_DATA"

   disable_copy_data_policy {
      enable = true  # Block data copy
   }
}

# Restrict SQL execution in SQL Editor from production
resource "bytebase_policy" "data_source_query_policy_prod" {
   depends_on = [bytebase_setting.environments]
   parent     = bytebase_setting.environments.environment_setting[0].environment[1].name
   type       = "DATA_SOURCE_QUERY"

   data_source_query_policy {
      restriction  = "RESTRICTION_UNSPECIFIED"
      disallow_ddl = true  # No schema changes via SQL Editor
      disallow_dml = true  # No data changes via SQL Editor
   }
}
```

- Here data protection policy is only applied to the `Prod` environment. Which means in `Test` environment, by default, users may execute DDL and DML statements or copy data directly in SQL Editor.

- `restriction` controls [access to the data source](/sql-editor/settings/data-source-restriction):

  - `RESTRICTION_UNSPECIFIED`: Admin data source is allowed.
  - `DISALLOW`: Admin data source is completely disallowed.
  - `FALLBACK`: Prefer the read-only data source; use admin only if read-only is not configured.

## Step 6 - Apply Configuration

Apply all configurations:

```bash
terraform plan
terraform apply
```

Verify in Bytebase:

1. Go to **Environments**.
2. Check that `Prod` shows a shield icon (protected).
3. Click each environment to see the configured policies.

## Summary and Next Steps

You've successfully configured Bytebase environments using Terraform! Your setup now includes:

- **Test environment**: Unprotected with automatic deployment for fast development
- **Prod environment**: Protected with manual deployment and data restrictions for safety

<Card
  title="Part 2: Manage Databases with Terraform"
  icon="arrow-right"
  href="/tutorials/manage-databases-with-terraform"
  horizontal
></Card>
